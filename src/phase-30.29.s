               JOB  Fortran compiler -- Stmt Numbers Four -- phase 30
               CTL  6611
     *
     * The three-character equivalents of statement numbers which
     * identify statements are matched against the statement number
     * table.  When the equivalent is found, the sequence number
     * generated by the compiler for that statement is substituted
     * in the table.  Unreferenced and multi-defined statement
     * numbers are noted.
     *
     x1        equ  89
     x2        equ  94
     x3        equ  99
     *
     * Stuff in the resident area
     *
     phasid    equ  110  Phase ID, for snapshot dumps
     seqtab    equ  148  Bottom of sequence number table - 2
     glober    equ  184  Global error flag -- WM means error
     snapsh    equ  333  Core dump snapshot
     loadnx    equ  700  Load next overlay
     clearl    equ  707  CS at start of overlay loader
     tpread    equ  780  Tape read instruction in overlay loader
     loadxx    equ  793  Exit from overlay loader
     clrbot    equ  833  Bottom of core to clear in overlay loader
     *
     bndry     equ  849  topcod + 0.48 * (bottab - 1 - topcod)
     move      equ  937  Move either prefix or statement up
     hash      equ  964  compute hash probe
     sought    equ  1051  label sought in hash probe routine
     sx1a      equ  1054
     seqcod    equ  1062
     savbot    equ  1065  bottom of sequence number table
     toobig    equ  1066
     sx1       equ  1143
     *
     110       dcw  @stnum for@
     *
               ORG  1187
     loaddd    equ  *&1          Load address
 1187beginn    LCA  kb1,0&X2
 1194          SW   gm
 1198          MCW  x1,x2
 1205nxstmt    BCE  done,0&X1,
 1213          MCW  0&X1,seqcod
 1220          SAR  x1
 1224          BCE  nolabl,0&X1,}
 1232          B    hash  compute hash probe
     * lookup in hash table
 1236          MCW  nop,switch
 1243lookl1    MCW  0&X1,x3
 1250          SAR  x1
 1254          BW   look2,1&X1
 1262          BCE  switch,3&X1,<  bottom of the table
 1270          BCE  unref,1&X1,  probed hash entry is empty
 1278          C    3&X1,sought
 1285          BU   lookl1
 1290          B    double
 1294look2     C    0&X3,sought
 1301          BU   lookl1
 1306          MZ   seqcod-1,svzone
 1313          MZ   *-4,seqcod-1
 1320          MCW  seqcod,0&X3
 1327          SBR  x3
 1331          CW   1&X3  no WM in label table means defined
 1335          MCW  3&X1,seqcod
 1342          MZ   svzone,seqcod-1
 1349          MCW  sought,3&X1
 1356          CW   1&X1  no WM in hash table means defined
 1360look3     MCW  sx1a,x1
 1367nolabb    SBR  x1,4&X1  Back here from nolabl
 1374          MCW  seqcod
 1378          B    move
 1382          B    move
 1386          B    nxstmt
     *
 1390switch    NOP  unref  got here twice?
 1394          MCW  branch,switch
 1401          MCW  bndry,x1
 1408          B    lookl1
     *
 1412unref     CS   332
 1416          CS
 1417          MN   seqcod,250
 1424          MN
 1425          MN
 1426          MCW  err19
 1430          W
 1431          BCV  *&5
 1436          B    *&3
 1440          CC   1
 1442          B    unref2
     *
 1446double    CS   332
 1450          CS
 1451          SW   glober
 1455          MN   seqcod,245
 1462          MN
 1463          MN
 1464          MCW  err20
 1468          W
 1469          BCV  *&5
 1474          B    *&3
 1478          CC   1
 1480          B    look3
     *
 1484done      MCW  savbot,x3
 1491          LCA  gm,0&X3
 1498          SBR  x3
 1502          SBR  seqtab
 1506          CS   0&X2
 1510          MCW  sx1,x1
 1517          SW   0&X2
 1521          BSS  snapsh,C
 1526          SBR  tpread&6,838
 1533          SBR  clrbot
 1537          SBR  loadxx&3,838
 1544          SBR  clearl&3,gmwm
 1551          LCA  stnum5,phasid
 1558          B    loadnx
     *
 1562unref2    MCW  sx1a,x1
 1569nolabl    BCE  *&5,seqcod-3,D  Do statement?
 1577          B    nolabb
 1581          MCW  savbot,x3
 1588          MCW  seqcod,0&X3
 1595          SBR  x3
 1599          BCE  toobig,0&X3,<
 1607          MCW  savbot,seqcod
 1614          SBR  savbot,1&X3
 1621          B    nolabb
 1625kb1       DCW  #1
 1626nop       NOP
 1627svzone    DCW  #1
 1628branch    B
 1675err19     DCW  @ERROR 19 - UNREFERENCED STMT NUMBER, STATEMENT @
 1717err20     DCW  @ERROR 20 - DOUBLY DEFINED STMT, STATEMENT @
 1724stnum5    DCW  @STNUM 5@
 1725gm        dc   @}@
 1726gmwm      DCW  @}@
               org  201
  203          dsa  loaddd    load address for card-to-tape program
               ex   beginn
               END
