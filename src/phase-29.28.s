               JOB  Fortran compiler -- Stmt Numbers Three -- phase 29
               CTL  6611
     *
     * The three-character equivalents of statement numbers
     * appearing within statements (generated by Statement
     * Numbers Phase One) are placed in a table.
     *
     * On entry, 83 is one below the bottom of the numbers, formats
     * and I/O lists in high core, and x1 and x2 are the top of
     * code in low core.
     *
     x1        equ  89
     x2        equ  94
     x3        equ  99
     *
     * Stuff in the resident area
     *
     phasid    equ  110  Phase ID, for snapshot dumps
     tblbot    equ  145  One below numbers, formats, I/O lists
     snapsh    equ  333  Core dump snapshot
     loadnx    equ  700  Load next overlay
     clearl    equ  707  CS at start of overlay loader
     cdovly    equ  769  1 if running from cards, N if from tape
     tpread    equ  780  Tape read instruction in overlay loader
     loadxx    equ  793  Exit from overlay loader
     clrbot    equ  833  Bottom of core to clear in overlay loader
     *
     topcd9    equ  840  top of code & x00 - 1
     diff16    equ  846  16 * (bottab - 1 - topcd9)
     bndry     equ  849  topcd9 + 0.48 * (bottab - 1 - topcd9)
     bottab    equ  852  input bottom of tables, output top of code & 2
     movedn    equ  853  move statements down
     *
     110       dcw  @stnum tri@
     *
               ORG  937
     loaddd    equ  *&1          Load address
     *
     * Move either prefix or statement up
     *
  937move      SBR  movex&3
  941          LCA  0&X1,0&X2
  948          SAR  x1
  952          C    0&X2
  956          SAR  x2
  960movex     B    0
     *
     * Compute hash probe for sought and leave it in x1.  Save
     * -3&x1 in sx1a.
     *
  964hash      SBR  hashx&3
  968          MCW  0&X1,sought
  975          SAR  sx1a
  979          MN   sought,w4
  986          MN
  987          MN
  988          MN
  989          S    diff16-2,w4
  996          BWZ  *-14,w4,B
 1004          A    diff16-2,w4
 1011          MZ   *-4,w4
 1018          MCW  w4,x1
 1025          A    x1
 1029          A    w4,x1
 1036savtop    NOP  0-0  why not just
 1040          SAR  x1     SBR X1,0-0 ?
 1044hashx     B    0
     *
 1048          DCW  #1
 1051sought    DCW  #3
 1054sx1a      DCW  #3
 1058w4        DCW  #4
 1062seqcod    DCW  #4
 1065savbot    DCW  #3  bottom of I/O strings, format, numbers - 1
     *
 1066toobig    CS   332
 1070          CS
 1071          CC   1
 1073          MCW  err2,270
 1080          W
 1081          CC   1
 1083          BCE  halt,cdovly,1
 1091          RWD  1
 1096halt      H    halt
     *
 1100done      MCW  sx1,x1
 1107          BSS  snapsh,C
 1112          SBR  tpread&6,beginn
 1119          SBR  clrbot
 1123          SBR  clearl&3,gmwm
 1130          LCA  stnum4,phasid
 1137          B    loadnx
 1143sx1       DCW  #3
 1179err2      DCW  @MESSAGE 2 - OBJECT PROGRAM TOO LARGE@
 1186stnum4    DCW  @STNUM 4@
     *
 1187beginn    MCW  83,savbot
 1194          MCW  83,tblbot
 1201          MCW  topcd9,savtop&3
 1208          MZ   x1zone,savtop&2  x1 zone
 1215          SBR  bottab,2&X2  bottab is not top of code & 2
 1222          MCW  x1,sx1
 1229nxstmt    MCW  0&X1,seqcod
 1236          BCE  compgo,seqcod-3,T  Computed GOTO?
 1244          MCW  branch,swich2
 1251          MCW  savbot,83
 1258          C    x1,x2
 1265          BE   tstend
 1270          B    movedn
 1274tstend    BCE  done,0&X1,
 1282          MCW  0&X1,seqcod
 1289          B    move  move up prefix
 1293          MCW  seqcod-3,*&8
 1300          BCE  labels,stmts,0
 1308          B
 1309          B
 1310          B
 1311          B
 1312          B
 1313cont      B    move  move up body
 1317          B    tstend
     *
     * An interesting statement -- one containing labels of other
     * executable statements.  Not an I/O statement containing a
     * format label.
     *
     * ??? This doesn't work for computed GOTO, because it doesn't
     * have a comma.  It ends up putting the variable in the label
     * table. ???
     *
 1321labels    B    hash
 1325          B    lookup
 1329          MCW  x3,0&X2  move table address to code
 1336          SBR  x2
 1340          MCW  sx1a,x1
 1347          BCE  cont,0&X1,}
 1355          BCE  cont,0&X1,,
 1363          B    labels
     *
     * Computed GOTO
     * ??? This looks buggy.  Computed GOTO doesn't have
     * a comma in it, and X3 is off by one. ???
     *
 1367compgo    C    0&X1  get to top of body
 1371          MN
 1372          SAR  x3
 1376          S    w3b
 1380compl1    MN   0&X3
 1384          MN
 1385          MN
 1386          SAR  x3
 1390          A    k1,w3b
 1397          BCE  *&5,1&X3,,
 1405          B    compl1
 1409          S    kp11,w3b
 1416          BWZ  compg2,w3b,B
 1424          B    move
 1428compfn    B    move
 1432          B    nxstmt
 1436compg2    MN   0&X1
 1440          MN
 1441          MN
 1442          MCW  kh
 1446          B    move
 1450          MCW  x3,sx3
 1457          MN   0&X3
 1461          MN
 1462          SAR  x1
 1466          A    kp9,w3b
 1473compl2    SBR  x1,6&X1
 1480          B    hash
 1484          B    lookup
 1488          MCW  sx1a,x1
 1495          BCE  *&5,4&X1,}  End of statement?
 1503          B    compl2
 1507          MCW  savbot,0&X2
 1514          MCW  w3b
 1518          SBR  x2
 1522          MCW  sx3,x1
 1529          B    compfn
     *
     * Lookup sought in the hash table.  Insert bottom of tables
     * address in hash table if not found, copy sought to
     * bottom of tables, and decrement bottom of tables address.
     *
 1533lookup    SBR  lookx&3
 1537          MCW  nop,switch  indicate first time
 1544lookl     MCW  0&X1,x3
 1551          SAR  x1
 1555          BCE  empty,3&X1,
 1563          BCE  switch,3&X1,<
 1571          C    0&X3,sought
 1578          BU   lookl
 1583swich2    NOP  lookx
 1587          MCW  savbot,0&X3
 1594          SBR  x3
 1598          MZ   kba,2&X3
 1605          CW
 1606empty     LCA  savbot,3&X1
 1613          MCW  savbot,x3
 1620          BCE  toobig,0&X3,<
 1628          B
 1629          B
 1630          LCA  sought,0&X3
 1637          SBR  savbot
 1641lookx     B    0
     *
 1645switch    NOP  toobig
 1649          MCW  branch,switch
 1656          MCW  bndry,x1
 1663          B    lookl
     *
 1667x1zone    dcw  @S@
 1668branch    B
 1674stmts     DCW  @WTDEGK@  codes for statements with labels
 1677w3b       DCW  #3
 1678k1        dcw  1
 1680kp11      DCW  &11
 1681kh        dcw  @H@
 1684sx3       DCW  #3
 1685kp9       DCW  &9
 1686nop       NOP
 1688kba       DCW  @ A@
 1689gmwm      DCW  @}@
     3199      dcw  #5
               org  201
  203          dsa  loaddd    load address for card-to-tape program
               ex   beginn
               END
